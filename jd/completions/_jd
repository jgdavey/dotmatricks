#compdef jd

local context state state_descr line
local ret=1
local -a list
local comps

_arguments -C \
           '1: :->cmds' \
           '*:: :->args' && ret=0

export COMP_WORD="${line[-1]}"

# Special-case some jd-commands
case "${line[1]}" in
  cd)
    _files -W "$SOURCE_DIR" -/
    return 0
  ;;
esac

# Or use the commands themselves
case $state in
  cmds)
    comps="$(_call_program jd-cmds jd completions --commands)"
    ;;
  args)
    comps="$(_call_program jd-args jd completions $line[@])"
    has="$?"
    ;;
esac

if [[ -z "$comps" ]]; then
  if [[ "$has" = 42 ]]; then
    _default # fallback to generic (usually filename) completion
  else
    _message "No completions"
  fi
else
  list=("${(ps:\n:)comps}")

  _values "jd-command" "${list[@]}"
fi

return ret
# Local Variables:
# mode: Shell-Script
# sh-indentation: 2
# indent-tabs-mode: nil
# sh-basic-offset: 2
# End:
# vim: ft=zsh sw=2 ts=2 et
